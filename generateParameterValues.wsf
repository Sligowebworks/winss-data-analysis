<job id="GenerateParameterValues">
  <script language="VBScript" src="generateUtilities.vbs"/>
  <script language="VBScript">
OPTION EXPLICIT

' takes one argument, a file name of a character-seperated-values
' Configure the field delimiter and the Stanza Delimiter
' Stanza Delimiter must start a line that names the stanza
' all lines after a stanza delimiter, up to another stanza will be in the stanza
' blank lines and spaces are ignored
' rows() array will be populated with values() arrays for each data row of csv
' InvokeGenerate() will call the sub Generate() which must be declared here
' Generate is called once for each stanza. the rows array is flushed after each call to Generate()
' Utilities declare TextStream, tsOut, for writing

' *** Required by Utilities:
' Configurations:
Const blnDEBUG = false
Const FIELD_ARRAY_MAX = 2 ' number fields in input csv - 1
Const NEW_STANZA_DELIM = "::"
Const FIELD_DELIM = "|"
Const FILE_EXT = ".cs"
' End Configurations

Dim stanzaName 'read from csv, 
ReDim values(FIELD_ARRAY_MAX)
Dim rows()	'dynammic array of values array (above). (column,row) Note: only last dimension is dynamic, hence col-row tuple instead of row-col
Dim strFileName	'from user
Dim rowCount	'number of rows in each stanza
' *** End Required by Utilities

Dim colArgs	'command line arguments

Set colArgs = WSH.Arguments
strFileName = CStr(colArgs(0))
Set colArgs = Nothing

Call InvokeGenerate	'defined in utilities include

'~,~`~,~`~,~`~,~`

sub Generate()
	debug "Writing Class::" & stanzaName, true
	if rowCount = 0 then
		Exit Sub
	end if
	
	Call GenerateHead
	Call GenerateEnum
	Call GenerateParameterClass
	Call GenerateFoot

end sub

sub GenerateEnum()
'create constants for validating user params
	Dim n
	
    tsOut.WriteLine vbTab & "public struct " & stanzaName & "Keys"
    tsOut.WriteLine vbTab & "{"
	n = 0
	do while n < rowCount
		tsOut.WriteLine vbTab & vbTab & "public const string " & rows(0,n) & " = """ & rows(1,n) & """;"
	n = n + 1
	loop
    tsOut.WriteLine vbTab & "}"
end sub

sub GenerateHead()
'using statements and namespace open
	tsOut.WriteLine "using System;"
	tsOut.WriteLine "using System.Collections.Generic;"
	tsOut.WriteLine "using System.Text;"
	tsOut.Write vbNewLine
	tsOut.WriteLine "namespace SligoCS.Web.WI.WebSupportingClasses.WI"
	tsOut.WriteLine "{"
end sub

sub GenerateFoot()
	tsOut.WriteLine "}"	' close namespace
end sub

sub GenerateParameterClass()
	Dim n
	
	tsOut.Write vbNewLine
	tsOut.WriteLine vbTab & "public class " & stanzaName & " : ParameterValues"
    tsOut.WriteLine vbTab & "{"
    tsOut.WriteLine vbTab & vbTab & "private static SerializableDictionary<String, String> range;"
	tsOut.Write vbNewLine
    tsOut.WriteLine vbTab & vbTab & "public override SerializableDictionary<string, string> Range"
    tsOut.WriteLine vbTab & vbTab & "{"
    tsOut.WriteLine vbTab & vbTab & vbTab &"get"
    tsOut.WriteLine vbTab & vbTab & vbTab &"{"
    tsOut.WriteLine vbTab & vbTab & vbTab & vbTab &"if (range == null) range = initRange();"
    tsOut.WriteLine vbTab & vbTab & vbTab & vbTab &"return range;"
    tsOut.WriteLine vbTab & vbTab & vbTab &"}"
    tsOut.WriteLine vbTab & vbTab & "}"

    tsOut.WriteLine vbTab & vbTab & "protected  SerializableDictionary<String, String> initRange()"
    tsOut.WriteLine vbTab & vbTab & "{"
    tsOut.WriteLine vbTab & vbTab & vbTab &"SerializableDictionary<String, String> range = new SerializableDictionary<String, String>();"
	tsOut.Write vbNewLine
	
	n = 0
	do while n < rowCount
		tsOut.WriteLine vbTab & vbTab & vbTab & "range.Add(" & stanzaName & "Keys." & rows(0,n) & ", """ & rows(2,n) & """);"
	n = n + 1
	loop
	tsOut.Write vbNewLine
	
	tsOut.WriteLine vbTab & vbTab & vbTab &"return range;"
    tsOut.WriteLine vbTab & vbTab & "}"
    tsOut.WriteLine vbTab & "}"
end sub
</script>
</job>